cmake_minimum_required(VERSION 2.6)
project(cvJiggle)

SET(MAYA_PLUGIN_NAME ${PROJECT_NAME})

SET(SOURCE_FILES "pluginMain.cpp"
    "cvJiggleCmd.cpp" "cvJiggleCmd.h"
    "cvJiggleDeformer.cpp" "cvJiggleDeformer.h")

SET(MAYA_VERSION 2012 CACHE STRING "Maya version")
SET(CUSTOM_DEFINITIONS "REQUIRE_IOSTREAM;_BOOL")
SET(MAYA_INSTALL_BASE_SUFFIX "")
SET(MAYA_INC_SUFFIX "include")
SET(MAYA_LIB_SUFFIX "lib")
IF(WIN32)
    # Windows
    SET(MAYA_INSTALL_BASE_DEFAULT "C:/Program Files/Autodesk")
    SET(CUSTOM_DEFINITIONS "${CUSTOM_DEFINITIONS};NT_PLUGIN")
    SET(OS_SYSTEM /win64/)
ELSEIF(APPLE)
    # Apple
    SET(MAYA_INSTALL_BASE_DEFAULT /Applications/Autodesk)
    SET(OS_SYSTEM /OSX/)
    SET(MAYA_INC_SUFFIX "devkit/include")
    SET(MAYA_LIB_SUFFIX "Maya.app/Contents/MacOS")
    SET(CUSTOM_DEFINITIONS "${CUSTOM_DEFINITIONS};OSMac_")
ELSE(WIN32)
    # Linux
    SET(MAYA_INSTALL_BASE_DEFAULT /usr/autodesk)
    SET(CMAKE_CXX_FLAGS "-fPIC")
    SET(OS_SYSTEM /Linux/)
    SET(MAYA_INSTALL_BASE_SUFFIX -x64)
ENDIF(WIN32)

SET(MAYA_INSTALL_BASE_PATH ${MAYA_INSTALL_BASE_DEFAULT} CACHE STRING
    "Path containing all your maya installations, like /usr/autodesk or /Applications/Autodesk/")

# OpenMP
FIND_PACKAGE(OpenMP) 
IF(OPENMP_FOUND)
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
    SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} ${OpenMP_EXE_LINKER_FLAGS}")
ENDIF()

SET(LIBRARIES ${LIBRARIES} "Foundation" "OpenMaya" "OpenMayaAnim")

SET(_MAYA_LOCATION ${MAYA_INSTALL_BASE_PATH}/maya${MAYA_VERSION}${MAYA_INSTALL_BASE_SUFFIX})
SET(_PROJECT ${PROJECT_NAME})

INCLUDE_DIRECTORIES(${_MAYA_LOCATION}/${MAYA_INC_SUFFIX})
LINK_DIRECTORIES(${_MAYA_LOCATION}/${MAYA_LIB_SUFFIX})

ADD_LIBRARY(${_PROJECT} SHARED ${SOURCE_FILES})
TARGET_LINK_LIBRARIES(${_PROJECT} ${LIBRARIES})

SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES COMPILE_DEFINITIONS "${CUSTOM_DEFINITIONS}")
SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES OUTPUT_NAME ${PROJECT_NAME})
SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES CLEAN_DIRECT_OUTPUT 1)

IF (WIN32)
    SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES SUFFIX ".mll")
    SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES LINK_FLAGS "/export:initializePlugin /export:uninitializePlugin")
    INSTALL(TARGETS ${_PROJECT} RUNTIME DESTINATION ${MAYA_MODULE_DIR}/plug-ins)
ELSEIF (APPLE)
    SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES SUFFIX ".bundle")
    INSTALL(TARGETS ${_PROJECT} LIBRARY DESTINATION ${MAYA_MODULE_DIR}/plug-ins)
    SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES PREFIX "")
ELSE (WIN32)
    INSTALL(TARGETS ${_PROJECT} LIBRARY DESTINATION ${MAYA_MODULE_DIR}/plug-ins)
    SET_TARGET_PROPERTIES(${_PROJECT} PROPERTIES PREFIX "")
ENDIF(WIN32)



